apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'project-report'

mainClassName = 'com.akonizo.splendor.stats'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.3'
    compile ( "com.orientechnologies:orientdb-graphdb:2.0.8" ) { exclude module:'commons-logging' }
    compile 'org.apache.commons:commons-math3:3.5'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'org.slf4j:slf4j-ext:1.7.12'

    runtime 'org.slf4j:jcl-over-slf4j:1.7.12'
    runtime 'org.slf4j:log4j-over-slf4j:1.7.12'
    runtime 'ch.qos.logback:logback-classic:1.1.3'

    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
}

// Ensure everything's using the same version of Groovy
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.codehaus.groovy') {
            details.useVersion '2.4.3'
        }
    }
}

// Disable Groovy optimizations when testing code coverage
gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(':jacocoTestReport')) {
        println "Disabling Groovy Optimizations for code coverage"
        compileGroovy.groovyOptions.optimizationOptions.all = false
    }
}

task "create-dirs" << {
   sourceSets*.java.srcDirs*.each { it.mkdirs() }
   sourceSets*.resources.srcDirs*.each { it.mkdirs() }
}
